source "$HOME/.asdf/asdf.sh"
fpath=(${ASDF_DIR}/completions $ZDOTDIR/completions $fpath)

# fpath=($ZDOTDIR/completions $fpath)
# eval "$(rtx activate zsh)"

# zsh rename thing omg this exists!
# https://unix.stackexchange.com/questions/1136/batch-renaming-files
autoload zmv

autoload -Uz compinit
zstyle ':completion:*' menu select
zmodload zsh/complist
compinit

# Include hidden files
_comp_options+=(globdots)

setopt hist_ignore_all_dups inc_append_history
HISTFILE="${XDG_CACHE_HOME:-$HOME/.cache}/zsh/history"
HISTSIZE=4000
SAVEHIST=4000

# Lines configured by zsh-newuser-install
setopt nomatch notify
unsetopt autocd beep extendedglob
# End of lines configured by zsh-newuser-install

bindkey -e

# The noclobber option makes the > throw an error
# when the destination file already exists.
# To force it, we have to use >| or >!
set -o noclobber

command_not_found_handler() {
  # https://askubuntu.com/questions/1226574/how-do-i-enable-zsh-package-suggestions-as-in-ubuntu-bash
  # calls the nice command-not-found handle function that shows a message
  # with package suggestions, just like it was in bash
  # If on debian, might have to install the `command-not-found` package
  # https://unix.stackexchange.com/questions/67466/how-to-implement-package-install-suggestion-on-debian
  if [ -x /usr/lib/command-not-found ]; then
    /usr/lib/command-not-found -- "$1"
  elif [ -x /usr/share/command-not-found/command-not-found ]; then
    /usr/share/command-not-found/command-not-found -- "$1"
  else
    echo "$1: Command not found" >&2
    return 127
  fi
}

autoload -Uz vcs_info
precmd() { vcs_info }
setopt PROMPT_SUBST

# Cores escuras / claras
# vermelho | verde | amarelo | azul | magenta | ciano | branco | preto/cinza
#   1/9    |  2/10 |   3/11  | 4/12 |   5/13  |  6/14 |  7/15  |     8/16
# https://zsh.sourceforge.io/Doc/Release/Prompt-Expansion.html#Prompt-Expansion

zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:git*' check-for-changes true
zstyle ':vcs_info:git*' unstagedstr '%F{11}'
zstyle ':vcs_info:git*' stagedstr '%F{14}'

# === Minimal one line prompt

zstyle ':vcs_info:git*+set-message:*' hooks my-git-remote-comparison-styling
zstyle ':vcs_info:git*' formats ' %F{9}%u%m%c%b'
PROMPT='%B%F{green}%~${vcs_info_msg_0_}%u%s%0(?.. %F{magenta}%?)%f%b λ '

# === Right prompt that shows the depth of shells, SHLVL, but only if is greater
#     than one when not inside tmux, and if greater then two if inside tmux.
#     This is because when inside tmux, the SHLVL is always at least two.

depth_not_counting_tmux() {
  if [ $TMUX ]; then
    [ $SHLVL -gt 2 ] && printf "depth:$SHLVL"
  else
    [ $SHLVL -gt 1 ] && printf "depth:$SHLVL"
  fi
}
RPROMPT='%B%F{6}$(depth_not_counting_tmux)%f%b'
# RPROMPT='%B%F{6}%2(L.{$SHLVL}.)%f%b'

# Unlike the asdf version, the rtx version is pretty fast
# I still don't use it, though
# rtx_lang_or_nothing() {
#   lang=`~/scripts/git-stuff/rtx-lang-version.sh`
#   if [ $lang ]; then
#     echo "%B%F{6}{$lang}%f%b"
#   fi
# }
# RPROMPT='$(rtx_lang_or_nothing)'

# ===  Big two line prompt

# zstyle ':vcs_info:git*' formats '%F{9}%u%c%m(%b)'
# zstyle ':vcs_info:git*+set-message:*' hooks my-git-remote-comparison-styling
# PROMPT='%B%F{green}%n@%m%F{white}:%F{blue}%~
# %B${vcs_info_msg_0_}%u%s%0(?..%F{magenta}[%?])%f%bλ '

+vi-my-git-remote-comparison-styling() {
  local git_status="$(git status 2> /dev/null)"

  case $git_status in
    *'No commits yet'*'nothing to commit'*)
      hook_com[misc]+='%F{white}'
      return
      ;;
    *ahead*)
      hook_com[misc]+='%S'
      ;;
    *diverged*)
      hook_com[misc]+='%K{red}%F{black} D %f%k '
      ;;
    *behind*)
      hook_com[misc]+='%U'
      ;;
  esac

  local unstaged_files=$(git status --porcelain 2>/dev/null)
  if [ -n "$unstaged_files" ]; then
    hook_com[misc]+='%F{11}'
  else
    hook_com[misc]+='%F{red}'
  fi
}

# short_os_name=$(grep ^ID= /etc/os-release | cut -b 4-6)
# RPROMPT='%B%F{6}{$short_os_name}%f%b'
# RPROMPT='%B%F{6}{zsh}%f%b'
# RPROMPT='%B%F{6}%*%f%b'

# This is cute, but too slow.
# It shows at the right the version of the programming language used at current
# project. Asdf is slow, though.
# lang_or_zsh() {
#   # lang=$(sh ~/scripts/git-stuff/asdf-lang-version.sh 2>/dev/null)
#   lang=$(sh ~/scripts/git-stuff/simplified-project-lang.sh)
#   echo ${lang:-zsh}
# }
# RPROMPT='%B%F{6}{$(lang_or_zsh)}%f%b'

# Run bindkey to see all bindings

# "To know the code of a key, execute cat, press enter, press the key, then Ctrl+C."
# https://stackoverflow.com/questions/8638012/fix-key-settings-home-end-insert-delete-in-zshrc-when-running-zsh-in-terminat
# Another way to know what a combination produces is to hit Control + V and then the combination/key.
bindkey "^[[3~" delete-char
bindkey "^[[H" beginning-of-line
bindkey "^[[F" end-of-line
bindkey "^H" backward-kill-word
bindkey "^[[3;5~" kill-word
# The "transpose-words" is such a nice command
# ^_ is "Control + /", although ESC+t and ESC+T are already mapped to transpose
bindkey "^_" transpose-words

bindkey "^F" clear-screen
bindkey "^T" kill-line
bindkey "^U" backward-kill-line

# OBS: Enter produz "^M"
# https://zsh.sourceforge.io/Doc/Release/Zsh-Modules.html#Menu-selection
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char
bindkey -M menuselect 'j' vi-down-line-or-history
bindkey -M menuselect '^M' accept-search
bindkey -M menuselect 'u' undo
bindkey -M menuselect 'i' accept-and-hold
bindkey -M menuselect 'a' accept-and-infer-next-history
bindkey -M menuselect 'q' send-break

# https://stackoverflow.com/questions/37772712/zsh-how-to-make-tab-completion-need-no-space-to-next-word-after-cursor
bindkey "^I" expand-or-complete-prefix
# bindkey "^I" complete-word

# https://stackoverflow.com/questions/815686/unable-to-make-a-backward-tab-as-shift-tab-in-zsh
bindkey '^[[Z' reverse-menu-complete # shift tab

# Control + esquerda/direita, respectivamente
bindkey ";5D" backward-word
bindkey ";5C" forward-word

# Dentro do tmux, control + esquerda/direita está gerando isso
bindkey "5D" backward-word
bindkey "5C" forward-word

# If ZDOTDIR is set, use it, otherwise, check if XDG_CONFIG_HOME is set, then use
# it followed by /zsh, and if not, use $HOME/.config/zsh
source "${ZDOTDIR:-${XDG_CONFIG_HOME:-$HOME/.config}/zsh}/plugins/fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh"

# Será que isso devia ficar aqui?
# Aumenta a quantidade de vezes que uma tecla é apertada quando seguramos ela
# xset r rate 220 25

eval "$(zoxide init zsh)"

source "$HOME/.aliases"
source "$HOME/.functions"

# https://superuser.com/questions/1585256/inserting-ssh-agent-into-i3wms-startup-so-its-available-to-all-processes-in-th
# eval $(keychain --eval --agents ssh --quick --quiet)

# https://www.tecmint.com/tips-for-tmux-terminal-sessions/

# Handle auto starting tmux and neofetch so
# they show messages in the right order.

if tmux ls 1>/dev/null 2>&1; then
  _is_there_a_tmux_session_running='sim'
fi

if ! [ $_is_there_a_tmux_session_running ]; then
  # This grep regex is to avoid not opening tmux when the process was something
  # like /usr/bin/alacritty instead of just alacritty
  if ps -o 'cmd=' -p $(ps -o 'ppid=' -p $$) | grep -q "^[/a-z0-9]*alacritty"; then
    `which tmux`
  fi
fi

# Putting this after tmux makes the startup faster when we open tmux,
# (no need to run neofetch before starting tmux and then running neofetch again),
# and the uptime section will not be outdated when we quit tmux,
# which probably happens when the neofetch is called before tmux.
neofetch --ascii "${XDG_CONFIG_HOME:-$HOME/.config}/neofetch/ascii-art" --ascii_colors 3 7

# But I want the message to appear after neofetch when there is already a tmux
# session running, but not if I am currently at tmux.
if [ $_is_there_a_tmux_session_running ] && [ -z "$TMUX" ]; then
  printf "Já existe pelo menos uma instância do \e[32mtmux\e[0m rodando:\n"
  . "$HOME/scripts/parse-tmux-ls.sh"
fi

unset _is_there_a_tmux_session_running
